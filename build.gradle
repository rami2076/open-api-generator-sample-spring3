plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id "org.openapi.generator" version "6.3.0"
}

apply plugin: 'io.spring.dependency-management'


group = 'nxy.non.com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '19'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.5'
}

openApiMeta {
    generatorName = 'Sample'
    packageName = 'org.openapitools'
    outputFolder = "${buildDir}/meta"
}

openApiValidate {
    inputSpec = "${rootDir}/specs/petstore-v3.0.yaml"
}

openApiGenerate {
    generatorName = 'spring'
    configFile = 'specs/config.json'
    inputSpec = "${rootDir}/specs/petstore-v3.0.yaml"
    outputDir = "${buildDir}/generated"
    apiPackage = 'org.openapitools.api'
    invokerPackage = 'org.openapitools.invoker'
    modelPackage = 'org.openapitools.model'
    configOptions = [
            dateLibrary   : "java8",
            interface     : "false",
            useJakartaEe  : "true",
            useSpringBoot3: "true"]
//    systemProperties = [
//            modelDocs: 'false'
//    ]
    typeMappings = [
            ByteStream : 'org.springframework.core.io.ByteArrayResource',
            InputStream: 'org.springframework.core.io.InputStreamResource',
    ]
    importMappings = [
            ByteStream : 'org.springframework.core.io.ByteArrayResource',
            InputStream: 'org.springframework.core.io.InputStreamResource',
    ]
    skipValidateSpec = true
    logToStderr = true
    generateAliasAsModel = false
}


tasks.named('test') {
    useJUnitPlatform()
}
compileJava.dependsOn tasks.openApiValidate, tasks.openApiGenerate
sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"
